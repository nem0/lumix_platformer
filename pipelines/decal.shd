include "pipelines/common.glsl"

texture_slot {
	name = "Texture",
	uniform = "u_texture",
	default_texture = "textures/common/white.tga"
}

uniform("Material color", "color", {1, 1, 1, 1})

------------------

vertex_shader [[
	layout(location = 0) in vec3 a_position;
	layout(location = 1) in vec3 i_pos;
	layout(location = 2) in vec4 i_rot;
	layout(location = 3) in vec3 i_half_extents;
	layout(location = 4) in vec2 i_uv_scale;
	layout(location = 0) out vec3 v_half_extents;
	layout(location = 1) out vec3 v_pos;
	layout(location = 2) out vec4 v_rot;
	layout(location = 3) out vec2 v_uv_scale;
	void main()
	{
		v_pos = i_pos;
		v_rot = i_rot;
		v_half_extents = i_half_extents;
		vec3 pos = rotateByQuat(i_rot, a_position * i_half_extents);
		pos += i_pos;
		v_uv_scale = i_uv_scale;
		gl_Position = Global.projection * Global.view * vec4(pos, 1);
	}
]]

---------------------


fragment_shader [[
	layout(location = 0) out vec4 o_gbuffer0;
	layout(location = 1) out vec4 o_gbuffer1;
	layout(location = 2) out vec4 o_gbuffer2;
	
	layout(location = 0) in vec3 v_half_extents;
	layout(location = 1) in vec3 v_pos;
	layout(location = 2) in vec4 v_rot;
	layout(location = 3) in vec2 v_uv_scale;
	
	layout (binding=0) uniform sampler2D u_texture;
	layout (binding=1) uniform sampler2D u_gbuffer_depth;
		
	void main()
	{
		vec2 screen_uv = gl_FragCoord.xy / Global.framebuffer_size;
		vec3 wpos = getViewPosition(u_gbuffer_depth, Global.inv_view_projection, screen_uv);
		
		vec4 r = v_rot;
		r.w = -r.w;
		vec3 lpos = rotateByQuat(r, wpos - v_pos);
		if (any(greaterThan(abs(lpos), v_half_extents))) discard;
		
		vec4 color = texture(u_texture, (lpos.xz / v_half_extents.xz * 0.5 + 0.5) * v_uv_scale);
		//if (color.a < 0.01) discard;
		color.rgb *= u_material_color.rgb;

		o_gbuffer0 = vec4(color.rgb, color.a);
		o_gbuffer1 = vec4(0, 0, 0, 0);
		o_gbuffer2 = vec4(0, 0, 0, 0);
	}
]]